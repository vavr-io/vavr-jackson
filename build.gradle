plugins {
    id 'java'
    id 'maven'
    id 'jacoco'
    id 'checkstyle'
    id 'com.bmuschko.nexus' version '2.3.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'io.vavr'
version = '1.0.0-alpha-3'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    compile "io.vavr:vavr:${vavrVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${fasterxmlVersion}"
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testCompile "javax.xml.bind:jaxb-api:${jaxbVersion}"
    testCompile "com.squareup:javapoet:${javapoetVersion}"
    testCompile "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${fasterxmlVersion}"
    testCompile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${fasterxmlVersion}"
    testCompile "com.fasterxml.jackson.datatype:jackson-datatype-joda:${fasterxmlVersion}"
    testCompile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${fasterxmlVersion}"
    testCompile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${fasterxmlVersion}"
    testCompile "com.fasterxml.jackson.module:jackson-module-scala_2.11:${fasterxmlVersion}"
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    sourceSets sourceSets.main

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = true
        csv.enabled = false
    }
}

codeCoverageReport.dependsOn {
    project.test
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'io.vavr.jackson'
        )
    }
}

modifyPom {
    project {
        name 'Vavr-Jackson'
        description 'Jackson datatype module for Vavr.io'
        url 'http://vavr.io'
        inceptionYear '2016'

        scm {
            connection 'scm:git:git@github.com:vavr-io/vavr-jackson.git'
            developerConnection 'scm:git:git@github.com:vavr-io/vavr-jackson.git'
            url 'git@github.com:vavr-io/vavr-jackson.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'ruslansennov'
                name 'Ruslan Sennov'
                email 'ruslan.sennov@gmail.com'
            }
            developer {
                id 'minconghuang'
                name 'Mincong Huang'
                email 'mincong.h@gmail.com'
            }
        }
    }
}

nexus {
    sign = !version.endsWith("SNAPSHOT")
}

test {
    useJUnitPlatform()
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}
