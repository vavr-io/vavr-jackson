plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'checkstyle'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

group = 'io.vavr'
version = '0.10.5-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    implementation "io.vavr:vavr:${vavrVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${fasterxmlVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "javax.xml.bind:jaxb-api:${jaxbVersion}"
    testImplementation "com.squareup:javapoet:${javapoetVersion}"
    testImplementation "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:${fasterxmlVersion}"
    testImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${fasterxmlVersion}"
    testImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${fasterxmlVersion}"
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-joda:${fasterxmlVersion}"
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${fasterxmlVersion}"
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${fasterxmlVersion}"
    testImplementation "com.fasterxml.jackson.module:jackson-module-scala_2.11:${fasterxmlVersion}"
}

tasks.register('codeCoverageReport', JacocoReport) {
    dependsOn tasks.test
    executionData.setFrom(fileTree(project.rootDir).include("**/build/jacoco/*.exec"))

    sourceDirectories.setFrom(files(sourceSets.main.allSource.srcDirs))
    classDirectories.setFrom(fileTree("build/classes/java/main") {
        exclude '**/SomeExcludedClass.class'
    })

    reports {
        xml.required.set(true)
        xml.outputLocation.set(file("${buildDir}/reports/jacoco/report.xml"))
        html.required.set(true)
        csv.required.set(false)
    }
}

tasks.test {
    finalizedBy tasks.codeCoverageReport
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'io.vavr.jackson'
        )
    }
}

test {
    useJUnitPlatform()
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}
